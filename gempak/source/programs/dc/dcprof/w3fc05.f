      SUBROUTINE W3FC05(U, V, DIR, SPD)
C$$$  SUBPROGRAM DOCUMENTATION BLOCK
C                .      .    .                                       .
C SUBPROGRAM:  W3FC05        EARTH U,V WIND COMPONENTS TO DIR AND SPD
C   PRGMMR: CHASE            ORG: NMC421      DATE:88-10-26
C
C ABSTRACT: GIVEN THE TRUE (EARTH ORIENTED) WIND COMPONENTS
C   COMPUTE THE WIND DIRECTION AND SPEED.
C   INPUT WINDS AT THE POLE ARE ASSUMED TO FOLLOW THE WMO
C   CONVENTIONS, WITH THE OUTPUT DIRECTION COMPUTED IN ACCORDANCE
C   WITH WMO STANDARDS FOR REPORTING WINDS AT THE POLE.
C   (SEE OFFICE NOTE 241 FOR WMO DEFINITION.)
C
C PROGRAM HISTORY LOG:
C   81-12-30  STACKPOLE, JOHN
C   88-10-19  CHASE, P.   ALLOW OUTPUT VALUES TO OVERLAY INPUT
C   89-01-21  R.E.JONES   CONVERT TO MICROSOFT FORTRAN 4.10
C   90-06-11  R.E.JONES   CONVERT TO SUN FORTRAN 1.3
C   91-03-30  R.E.JONES   SiliconGraphics FORTRAN
C   93-03-29  R.E.JONES   ADD SAVE STATEMENT
C
C USAGE:    CALL W3FC05 (U, V, DIR, SPD)
C
C   INPUT ARGUMENT LIST:
C     U        - REAL*4 EARTH-ORIENTED U-COMPONENT
C     V        - REAL*4 EARTH-ORIENTED V-COMPONENT
C
C   OUTPUT ARGUMENT LIST:      (INCLUDING WORK ARRAYS)
C     DIR      - REAL*4 WIND DIRECTION, DEGREES.  VALUES WILL
C                BE FROM 0 TO 360 INCLUSIVE.
C     SPD      - REAL*4 WIND SPEED IN SAME UNITS AS INPUT
C
C   INPUT FILES:   NONE
C
C   OUTPUT FILES:  NONE
C
C   SUBPROGRAMS CALLED:
C     LIBRARY:
C       COMMON   - SQRT, ATAN2
C
C REMARKS:  IF SPEED IS LESS THAN 1E-10 THEN DIRECTION WILL BE SET
C   TO ZERO.
C
C ATTRIBUTES:
C   LANGUAGE: SiliconGraphics 3.5 FORTRAN 77
C   MACHINE:  SiliconGraphics IRIS-4D/25, 35, INDIGO
C
C$$$
C
C     VARIABLES.....
C
      REAL   U, V, DIR, SPD, XSPD
C
C     CONSTANTS.....
C
      SAVE
C
      DATA  SPDTST/1E-10/, RTOD/57.2957795/, DCHALF/180.0/

      XSPD = SQRT(U * U + V * V)
      IF (XSPD .LT. SPDTST) THEN
         DIR = 0.0
      ELSE
         DIR = ATAN2(U,V) * RTOD + DCHALF
      ENDIF
      SPD = XSPD
      RETURN
      END
